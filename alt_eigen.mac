/*  Copyright 2011, 2023 by Barton Willis

    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License version 3, or 
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License along with 
    this program. If not, see <https://www.gnu.org/licenses/>. */

(load('lrats), load('simplifying), load('integer_sequence));

/* True iff the expression e is free of floating point (both binary64 and bigfloat)
   numbers. */
float_free_p(e) := if mapatom(e) then not (floatnump(e) or bfloatp(e)) else every('float_free_p, args(e));

simp_assuming(pred,f) := block([prederror : false, bool : is(pred)],
  if bool = true then f 
  else if bool = false then 'unknown
  else simpfuncall('assuming, pred,f));

simplifying('assuming,'simp_assuming)$

/* A simplifying span function. So that it doesn't interfer with the linear algebra
   package, for now, we'll name it sspan (for simplifying span), not span.

   Removes vectors that are explicitly zero, normalizes vectors by removing 
   the gcd of its members, removes explicitly redundant vectors, chooses between
   the vector and its negation, and sorts the vectors. This could do more 
   (optionally do Gram-Schmidt, for example), but for now, this is OK.*/

   simp_sspan([v]) := block([vv : [], normalize],
      normalize : lambda([q], block([w, algebraic : true],
       w : xreduce('gcd,list_matrix_entries(q)),
       q : if w = 0 then q else ratsimp(q/w),
       second(sort([q,-q])))),
   for vk in v do (
     if some(lambda([q], q # 0), vk) then push(normalize(vk), vv)),
  simpfunmake('sspan, listify(setify(vv))));
simplifying('sspan,'simp_sspan)$

/* This is mostly column space code from the linear algebra package minus checking
   for correct inputs and the provios feature. Here p should be an equation of the
   form X = Y. After trigangularation, we fullratsubst p into the result. */
alt_eigen_columnspace(a,p) := block([m,nc, nr, cs : set(), pos, algebraic : true, c_min],
  [nr, nc] : matrix_size(a),
  m : fullratsubst(p,triangularize(a)),
  m : block([rootsconmode : 'all], matrixmap('rootscontract,m)),*/
  m : fullratsubst(p,m),
  m : matrixmap('rootscontract,m),
  m : matrixmap('radcan, m),
  print("m = ",m),
  c_min : 1,
  for i : 1 thru nr while c_min <= nc do (
   if listp(pos : locate_matrix_entry(m, i, c_min, i, nc, lambda([x], x # 0), 'bool)) then (
     c_min : second(pos) + 1,
     cs : adjoin(col(a,second(pos)) ,cs))),
   funmake('sspan, listify(cs)));

/* Return the nullspace of the matrix m. The second argument, p, should be an
   equation of the form XXX = YYY. Following a triangularization of m^T | I
   (augment the matrix m^T with the appropriately sized identity matrix),
   do a lratsubst of p of the triangularized matrix. Let's call
   the result of that A | B. 
   
   The return value is a list of lists. For each row of A that is syntactically zero
   or possibly zero,  include the list member [pred, transpose of the corresponding 
   row of B]; Say if the row of A is [0,0,a], we include the list member
   [equal(a,0), matrix([b1],[b2],[b3])].  

   This code is algorithmically the same as the nullspace method in the linear 
   algebra package, but the logic is somewhat simplified. The linear algebra package 
   issues facts about possible nonzero rows using proviso, but this method uses the
   assuming object. And the linear algebra package nullspace function doesn't 
   have the feature of reducing the result of triangularization by a kernel (p)
   that vanishes.

  The alt_eigen_nullspace function doesn't check that its arguments are what they need to
  be--alt_eigen_nullspace isn't intended to be a user-level function.
  
  See https://en.wikipedia.org/wiki/Kernel_(linear_algebra). */
alt_eigen_nullspace(m,p) := block([nc, nr, acc : [], r1, r2, ok, matrix_element_transpose : false],
   [nc,nr] : matrix_size(m), /* [nr,nc] = size of m^T */
   m : triangularize(addcol(transpose(m), ident(nr))),
   m : lratsubst(p, m),
   for r in m do (
      r1 : map(lambda([i],equal(inpart(r,i),0)), 1 .. nc),
      ok : block([prederror : false], xreduce("and",r1)),
      if ok # false then (
         r2 : map(lambda([i],inpart(r,i)), (nc+1) .. (nr+nc)),
         acc : push([ok, transpose(r2)],acc))),
   acc);
   
/* Apply the Gram-Schmidt process to the list of column vectors of equal size 
   (matrices) in v. The second argument p should be an equation of the form 
   XXX = YYY. Following each projection, substitute (using lratsubst) p into 
   the result of the projection. 

   The inner product is a.b = transpose(conjugate(a)).b. To do this, we'll locally set 
   matrix_element_transpose to conjugate.

   This function isn't intended to be an user-level function. Accordingly, it doesn't
   check that its arguments are OK. Generally, inappropriate arguments result in
   an unclear error message. */

alt_eigen_gramschmidt(v,p,orthogonal) := block([u : [], basis : [], proj, dot, uk, 
  doallmxops : true, scalarmatrixp : true, matrix_element_transpose : 'conjugate],   
   dot : lambda([a,b], transpose(a).b),
   proj : lambda([a,b], lratsubst(p, dot(a,b)*a/dot(a,a))), 
   for vk in v do (   
      vk : lratsubst(p,vk),      
      uk : ratsimp(vk - lsum(proj(s,vk), s, u)),
      uk : lratsubst(p,uk),
      if some(lambda([s], is(s # 0)), uk) then (
         push(uk, u),
         push(vk, basis))),
  apply(sspan, if orthogonal then u else basis));

/* It's not checked, but the first argument p should be a polynomial in the 
   variable z. When degree(p) <= deg, attempt to find the zeros of p. When
   that is successful, return a list of the solutions; and when solve is
   not successful, solve p for its highest power and return a one member
   list of solutions.*/
maybe_solve(p,z,deg) := block([n, lt, inflag : true, sol,  programmode : true, globalsolve : false, 
   solveexplicit : true, breakup : false, solvenullwarn : false, solvefactors : true],   
   p : ratexpand(p), 
   n : hipow(p,z), 
   sol : if n <= deg then solve(p,z) else [],
   if sol = [] then (    
      lt : ratcoeff(p,z,n),
      [z^n = (lt * z^n - p)/lt])
   else sol)$

/* Let m be a r x r matrix and let p be a polynomial in the variable z. This  function returns a basis for the  
solution to m v = mu * v, where mu is  a zero of the polynomial p. The basis is encoded as the columns of
a matrix.

Instead of handing alt_eigen_h the characteristic polynomial, it's possible to build up the characteristic polynomial 
in the while n > 0 loop. Once the loop finishes, the requirement that lk vanishes determines the characteristic 
polynomial (by solving linear equations).

ratmx : true misbehaves with a non default value or matrix_element_mult. And
ratmx : true doesn't seem to work with nullspace. Let's locally set ratmx to 
false for the call to nullspace.

bug: (can cause trouble for matrices with floating point entries)

 (%i4) rat(1.2) * rat(1/2) - 11/5, keepfloat : true;
 (%o4)/R/ -8.0/5
 (%i5) expand(%,0,0);

For algebraic numbers, sometimes subres is better than spmod.*/

alt_eigen_h(m,r,p,z) := block([vn, n, l, lk, ls, e : funmake('matrix,[]), 
    psub, q, listarith:true, scalarmatrixp:false, doallmxops : true, 
    prederror : false],
  vn : rat(ident(r)),
  p : rat(p), 
  n : hipow(p,z),  
  p : p / ratcoeff(p,z,n),
  psub : solve(p, z^hipow(p,z)),
  n : n-1,
  l : vn * z^n,
  lk : vn,
  while n > -1 do (
     lk : m . lk + ratcoeff(p, z, n) * vn,  
     n : n -1,
     if n > -1 then l : l + z^n * lk), 
  lk : alt_eigen_nullspace(lk,psub),
  lk : sublist(lk, lambda([q], first(q)=true)),
  lk : map('second, lk),
  for ls in lk do (
     ls : l . ls,     
     e : addcol(e, ls)),
  e);

/* Return true iff the expression e has the form a^b */
mexptp(e) := block([inflag : true],
  not mapatom(e) and inpart(e,0) = "^");

/* Return true iff the expression e has the form a*b */
mtimesp(e) := block([inflag : true],
  not mapatom(e) and inpart(e,0) = "*");

/* Return a simplified expression that is semantically the same as notequal(e,0).
   We could define a simplifying notequal, but let's stick with this. We do

   a) gfactor e and replace every factor q that satisfies notequal(q,0)=true by 1.

   b) gfactor e again and replace every factor of the form X^n, where is is a 
      positive number by X. */
simp_notequal(e,z) := block([inflag : true, fn],
  fn : lambda([q], if mexptp(q) and numberp(second(q) and second(q) > 0)
             then first(q) else q),
  /* Replace nonzero factors of e by one.*/
  e : gfactor(e),
  if mtimesp(e) then (
     e : map(lambda([q], if is(notequal(q,0))=true then 1 else q),e)),
 
  /* Replace factors of e of the form X^n where n is a positive number by X */
  e : gfactor(e),
  if mtimesp(e) then (
      e : map(fn,e)),
  /* Convert X^n, where n is a positive number by X */
  if mexptp(e) then (
     e : fn(e)),
   notequal(e,0));

/* The first argument is a list of the factors of a polynoimal and the
   second arument is the polynomial variable. Require that 

     a) each factor be square free
     b) the set of factors have pairwise non vanishing resultants
   
   Return either true or a notequal expression.

   Example: The condition that the polynomial z -> z^2-2*z-a*conjugate(a)+1 be square 
   free is a*conjugate(a)=0. So return notequal(a*conjugate(a),0). */
alt_eigen_cnd(p,z) := block([cnd,n],
   cnd: map(lambda([q],block([],
         q : gfactor(q/gcd(q, diff(q,z))),
         gfactor(resultant(q,diff(q,z),z)))), p),

   n : length(p),
   for k : 1 thru n-1 do (
      for l : k+1 thru n do (
         push(resultant(inpart(p,k),inpart(p,l),z), cnd))),
   cnd : delete(0,cnd),
   cnd : gfactor(xreduce("*",cnd)),
   simp_notequal(cnd,z));

/* Find the eigenvectors and eigenvalues of a square unblocked matrix; specifically:

    alt_eigen(m, 'var = z, 'maxdegree = n, 'orthogonal = bool)

maxdegree -- try to solve factors of the characteristic poly with degree n or less,
orthogonal -- when true, eigenvectors with geometric multiplicity two or more are 
sent through gram schmidt.

Computing the characteristic polynomial is much faster with ratmx : true. 

When gcd = spmod (the default) and algebraic is true, alt_eigen fails on the matrix 
matrix([1,1/2,%i],[0,1,1/2],[0,1/2,1+%i]). This bug is reported as bug
#4169 spmod bug when algebraic is true. Additionally, with the default values for
gcd (spmod) and algebraic (false), we have
   p :  z^2 + (2*%i - 2)*z - 2*%i, gcd(p, diff(p,z)) # z+%i-1.
So let's set gcd : subres and algebraic : true. */

alt_eigen(m, [extra]) := block([r, c, n, z : gensym(), p, lk, ord, hp, es : [], 
  u, ortho, ls, inflag : true, doallmxops : true, gcd : subres, algebraic : true],  
  if not matrixp(m) then error("The first argument must be a matrix"),  
  if blockmatrixp(m) then error("The first argument must be an unblocked matrix"),
  if not float_free_p(m) then error("The function alt_eigen does not work for matrices with floating point entries"),

  /* Basic check of keyword arguments. */
  if some(lambda([q], mapatom(q) or not (inpart(q,0)="=")),extra) then (
     error("Every optional argument must have the form a=b; found ", extra)),
  
  if not subsetp(setify(map('lhs, extra)), set('var,'maxdegree,'orthogonal)) then (
     error("Keyword arguments must be 'var','maxdegree', or 'orthogonal', found ", extra)),

  [r,c] : matrix_size(m),
  if r # c or r = 0 then error("The first argument must be a nonempty square matrix"),
  n : r,   
  hp : assoc('maxdegree, extra, 1), /* hp = highest power polynomial that will be solved */
  if not integerp(hp) then error("The value of 'maxdegree' must be an integer"),
  u : assoc('var, extra, z),
  
  if not mapatom(u) then (
    error("Eigenvalue identifier must be a mapatom; found ", u)),

  ortho : assoc('orthogonal, extra, false),
  if not freeof(u,m) then error("The first argument must not depend on ", u),

  p : block([ratmx : true], charpoly(matrixmap('rat, m), z)),  
  p : gfactor(p),
  p : if not mapatom(p) and op(p) = "*" then args(p) else [p],
  p : sublist(p, lambda([s], not freeof(z,s))), 
  p : map(gfactor,p),
  cnd : alt_eigen_cnd(p,z),
/*
  cnd : outermap(lambda([a,b], resultant(a,b,z)), p, p),
  cnd : xreduce('append, cnd),
  cnd : delete(0,cnd),
  cnd : listify(setify(cnd)),
  cnd : factor(xreduce("*",cnd)),
  cnd : simp_notequal(cnd,z), */
  
  for pk in p do ( 
    [pk,ord] : if not mapatom(pk) and op(pk) ="^" then args(pk) else [pk,1],
    lk : alt_eigen_h(m,r,pk,z),
    lk : lratsubst(first(maybe_solve(pk,z,1)), lk), 
    pk : maybe_solve(pk, z, hp),
    lk : alt_eigen_columnspace(lk, pk),
    for pkl in pk do (
       ls : subst(pkl, lk),
       if ortho then ls : alt_eigen_gramschmidt(ls, pkl,true),
       es : cons([pkl, ls], es))),
  assuming(cnd, subst(z=u,es)));